/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.3/userguide/tutorial_java_projects.html
 */

// define the group of the GAV
group = 'com.rajendarreddyj'

// define the version of the GAV
version = 1.0

description = "Root project of the Sping projects that uses Gradle."


allprojects {

	//Instructions for all projects
	ext {
		// compile/runtime dependency versions
		javaVersion="1.8"
		javaeeVersion = "7.0"
		javaxServletVersion = "3.1.0"
		springFrameworkVersion = "4.3.5.RELEASE"
	    springBatchVersion="3.0.7.RELEASE"
		springSecurityVersion = "4.2.1.RELEASE"
		springBootVersion = "1.5.9.RELEASE"
		springSessionVersion="1.3.1.RELEASE"
		hazelcastVersion = "3.8.2"
		commonsdbcpVersion="1.4"
		logbackVersion = "1.1.8"
	    aspectjVersion="1.8.10"
		toolingApiVersion = "3.3"
		guavaVersion = "23.0"
		commonsLang3Version="3.7"
		commonsIOVersion="2.6"
		jacksonVersion="2.9.4"
		gsonVersion = "2.2.4"
		slf4jVersion = "1.7.2"
		groovyVersion = "2.4.5"
		junitVersion = "4.12"
		hamcrestVersion = "1.3"
		spockVersion = "1.0-groovy-2.4"
		equalsverifierVersion = "1.5.1"
		cglibVersion = "2.2"
		objenesisVersion = "1.2"
	    hsqldbVersion="1.8.0.7"
	    tomcatVersion = "7.0.59"
	    tomcatPluginVersion="2.2.5"
	}
}

subprojects {
	//Instructions for each sub project
	// Apply the java plugin to add support for Java
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	group = rootProject.group
	version = rootProject.version
	sourceCompatibility = "${javaVersion}"
	targetCompatibility = "${javaVersion}"
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}
	// In this section you declare where to find the dependencies of your project
	repositories {
		// Use jcenter for resolving your dependencies.
		// You can declare any Maven/Ivy/file repository here.
		jcenter()
		mavenLocal()
		mavenCentral()
		maven {
			url "http://repo.maven.apache.org/maven2"
		}
	}

	buildscript {
		repositories {
			jcenter()
		    mavenLocal()
			mavenCentral()
			maven {
				url "http://repo.maven.apache.org/maven2"
			}
		}
		dependencies {}
	}

	dependencies {
		// The production code uses Guava
		compile "com.google.guava:guava:${guavaVersion}"
		
		// Use JUnit test framework
		testCompile "junit:junit:${junitVersion}"
	}

	configurations.all {
		it.exclude group: 'org.slf4j',	module: 'slf4j-log4j12'
		it.exclude group: 'org.slf4j',	module: 'slf4j-jdk14'
		it.exclude group: 'log4j',	module: 'log4j'
		//it.exclude group: 'commons-logging',	module: 'commons-logging'
	}

	configurations.all {
		resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
			failOnVersionConflict()

			// prefer modules that are part of this build (multi-project or composite build) over external modules
    		preferProjectModules()

            // force certain versions of dependencies (including transitive)
            //  *append new forced modules:
            
            
            //  *replace existing forced modules with new ones:
			forcedModules = [
				// spring framework force to latest version
				"org.springframework:spring-context:${springFrameworkVersion}",
    			"org.springframework:spring-aop:${springFrameworkVersion}",
    			"org.springframework:spring-beans:${springFrameworkVersion}",
				"org.springframework:spring-core:${springFrameworkVersion}",
				"org.springframework:spring-tx:${springFrameworkVersion}",
				// logback-classic uses logback 1.7.6
				"org.slf4j:slf4j-api:${slf4jVersion}",
				// junit uses hamcrest 1.1 
				"org.hamcrest:hamcrest-core:${hamcrestVersion}",
				// spock uses groovy 2.4.1 
				"org.codehaus.groovy:groovy-all:${groovyVersion}" 
			]
		}
	}

}

// Running 'gradle wrapper' will generate gradlew - Getting gradle wrapper working and using it will save you a lot of pain.
task wrapper(type: Wrapper) {
	gradleVersion = '3.3'
}
